
global has_generator_part_map: bool;
global has_generator_part_0: bool;
global has_generator_part_1: bool;
global has_generator_part_2: bool;
global has_repaired_generator: bool;

global has_health_juice_map: bool;

scene characters_brother: entity_id;
scene characters_brother_healed: entity_id;
scene repairs_generator_broken: entity_id;
scene medical_machine: entity_id;
scene scrapbot: entity_id;
scene near_med_machine: entity_id;
scene bedside: entity_id;
scene repairs_tablet_broken: entity_id;
scene corner: entity_id;

scene did_injure: bool;
scene is_samson_sad: bool;

func room_house()
    if not did_injure and samson_location == SAMSON_LOCATION_BED then
        npc_animate characters_brother, "injured", true;
        did_injure = true;
    end

    if has_tablet_note and not has_tablet_battery_map then
        has_tablet_battery_map = true;
        has_tablet_memory_map = true;
        has_tablet_screen_map = true;
    end
end

func on_enter()
    if has_repaired_generator and not has_health_juice_map then
        cam_animate "power_back";
        world_pause;
        interact_with_npc INTERACTION_LOOK, scrapbot, medical_machine;
        cam_wait;
        interact_with_npc INTERACTION_LOOK_MOVE, ENTITY_ID_PLAYER, near_med_machine;
        npc_set_speed ENTITY_ID_PLAYER, 3;
        npc_wait scrapbot;
        say "The power is back online.";
        npc_wait ENTITY_ID_PLAYER;
        interact_with_npc INTERACTION_LOOK, scrapbot, ENTITY_ID_PLAYER;
        cam_look_npc scrapbot;
        say "
            Although it appears the health juice went bad while the power was out.

            You will need to bring back more health juice.

            Here is a map where you can find some south east of here.
        ";
        world_unpause;
        cam_return;
        has_health_juice_map = true;
        show_item ITEM_HEALTH_JUICE_MAP;
        return;
    end
end

func on_brother_talk()
    world_pause;

    if has_given_tablet then
        cam_look_npc ENTITY_ID_SUBJECT;
        interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, ENTITY_ID_SUBJECT;
        interact_with_npc INTERACTION_LOOK, ENTITY_ID_SUBJECT, ENTITY_ID_PLAYER;

        say "
            Things are going to be okay.

            Thank you jax.
        ";
    elif has_tablet and not has_given_tablet then
        cam_look_npc ENTITY_ID_SUBJECT;
        interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, ENTITY_ID_SUBJECT;
        interact_with_npc INTERACTION_LOOK, ENTITY_ID_SUBJECT, ENTITY_ID_PLAYER;

        has_given_tablet = true;

        say "
            What is this?

            ...

            You fixed that old broken tablet.

            Is that mom and dad?

            I remember now. They wouldn't want me to lose hope like this.

            We can make it Jax. I know we can if we stick together.

            Thank you jax.
        ";

        fade FADE_COLOR_BLACK, 2.0;
        delay 2.5;

        say "Repair complete";

        delay 1.0;

        say "
            Created by team Ultrarare

            James Lambert
            Pyr0xene
            SapphireOnze

            terzdesign
            kaelin

            Thank you for playing
        ";
        
        fade FADE_COLOR_NONE, 2.0;
    elif samson_location == SAMSON_LOCATION_HEALED then
        if is_samson_sad then
            say "It hurts";
        else
            say "
                I don't remember what mom and dad looked like anymore.

                Are we doomed?    
            ";
        end
    else
        say "It hurts";
    end

    world_unpause;
    cam_return;
end

func on_robot_talk()
    world_pause;
    interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, ENTITY_ID_SUBJECT;
    interact_with_npc INTERACTION_LOOK, ENTITY_ID_SUBJECT, ENTITY_ID_PLAYER;
    cam_look_npc ENTITY_ID_SUBJECT;

    if not has_generator_part_map then
        say "
            There is a problem.

            The medical machine has lost power.
        ";
        say "
            The generator needs to be repaired.

            Follow me outside.
        ";

        scrapbot_location = SCRAPBOT_LOCATION_GENERATOR;
        fade FADE_COLOR_BLACK, 0.5;
        delay 0.5;
        world_unpause;
        cam_return;
        load_scene "rom:/scenes/overworld.scene#generator";
        return;
    elif has_generator_part_0 and has_generator_part_1 and has_generator_part_2 then
        if has_repaired_generator then
            say "Use the map to locate more health juice for the machine";
        else
            say "
                The medical machine is back online.

                You need to follow that new map I gave to get more health juice.
            ";
        end
    else
        say "After you aquire the parts return here to fix the generator";
    end

    world_unpause;
    cam_return;
end

func on_exit_medical_machine()
    if has_repaired_health_machine and samson_location == SAMSON_LOCATION_BED then
        world_pause;
        cam_look_npc scrapbot;
        interact_with_npc INTERACTION_LOOK, scrapbot, medical_machine;
        interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, scrapbot;
        say "
            The machine is functional again.

            I will tend to his wounds now.
        ";
        samson_location = SAMSON_LOCATION_HEALED;
        fade FADE_COLOR_BLACK, 0.5;
        delay 1.5;
        load_scene "rom:/scenes/inside_house.scene#samson_heal";
    end
end

func on_samson_heal()
    cam_look_npc characters_brother_healed;
    interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, characters_brother_healed;
    interact_with_npc INTERACTION_LOOK, characters_brother_healed, ENTITY_ID_PLAYER;
    interact_with_npc INTERACTION_LOOK, scrapbot, characters_brother_healed;
    fade FADE_COLOR_NONE, 0.5;
    delay 0.5;

    say "
        Thank you Jax.

        I wasn't sure if I was going to make it

        ...

        but maybe we are just delaying the inevitable.

        It has been so hard since mom and dad died. I don't think I remember what their faces look like anymore.

        I need to be alone for a minute.
    ";

    npc_set_speed characters_brother_healed, 2.0;
    interact_with_npc INTERACTION_LOOK_MOVE, characters_brother_healed, corner;
    is_samson_sad = true;
    delay 2.0;
    cam_look_npc scrapbot;

    interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, scrapbot;
    interact_with_npc INTERACTION_LOOK, scrapbot, ENTITY_ID_PLAYER;
    cam_look_npc scrapbot;
    npc_wait scrapbot;

    say "You saved his life but it appears his spirit is still broken";
    
    interact_with_npc INTERACTION_LOOK, ENTITY_ID_PLAYER, repairs_tablet_broken;
    interact_with_npc INTERACTION_LOOK, scrapbot, repairs_tablet_broken;
    cam_look_npc repairs_tablet_broken;

    say "
        I finished searching potential locations where you may find parts to repair your old tablet

        Perhaps that will have something to cheer samson up
    ";

    world_unpause;
    cam_return;
end
